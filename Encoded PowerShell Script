//List office process
let office_files = dynamic(['winword.exe','excel.exe','powerpnt.exe','outlook.exe']);
DeviceProcessEvents
//commandline matches encoded regex
| where ProcessCommandLine matches regex @'(\s+-((?i)encod?e?d?c?o?m?m?a?n?d?|e|en|enc|ec)\s).*([A-Za-z0-9+/]{50,}[=]{0,2})'
//decode encoded comandline 
| extend DecodedCommand = replace(@'\x00','', base64_decode_tostring(extract("[A-Za-z0-9+/]{50,}[=]{0,2}",0 , ProcessCommandLine))) 
//look for office process parent file
| where InitiatingProcessFileName has_any (office_files)
| project Timestamp,DeviceId,DeviceName,AccountName,InitiatingProcessFileName,FileName,DecodedCommand,ProcessCommandLine,ReportId
